
Document ← :'\n'* Tableofcontents? Paperroll

Tableofcontents ← :'[TOC]'
Paperroll ← (:'\n' | Block)*

### Block Elements

Block ↖ Block_Prefixed | Paragraph | Invalid_Block
Block_Prefixed ↖ Slides | Header | Blockquote | Codeblock | Linknote | Filetree | List | Table | Info | Warning | Factsheet

Slides ← :'---' :' '? Slides_Id? :'\n' ((Slides_Multislide | Slides_Item) :'--\n'?)* :'---'
Slides_Id ← (!'\n' .)+
Slides_Multislide ← Slides_Item (:'-↓\n' Slides_Item)+
Slides_Item ← (!('--\n' | '---\n' | '-↓\n') (:'\n' | Block))+

Header ↖ Header_L3 | Header_L2 | Header_L1
Header_L3 ← :'###' :' '? (!'\n' Span)*
Header_L2 ← :'##' :' '? (!'\n' Span)*
Header_L1 ← :'#' :' '? (!'\n' Span)*

Blockquote ← ( :'>' [^\n]* '\n' )+ ↺ .*

Codeblock ← :'```' Codelanguage :'\n' (Codeimport | Codeinline)
Codeimport ← :'```↑' (:' '* !'\n' Codeimport_Option)+
Codeimport_Option ← (![ \n] .)+
Codeinline ← (!'\n```' .)* :'\n```'
Codelanguage ← (!'\n' .)*

Linknote ← :'[' Link_Text :']: ' Link_Url :[ \t]*

Filetree ← :'🌲' (:' ' Filetree_Is_Auto_Link? Filetree_Basepath)? :'\n' Filetree_Root
Filetree_Is_Auto_Link ← :'↑'
Filetree_Basepath ← [^\n]*
Filetree_Root ← (Filetree_Item :'\n'?)+
Filetree_Item ← Filetree_Item_File (:' '* Filetree_Item_Description)? (:'\n' Filetree_Item_Children)?
Filetree_Item_File ← [^ \n]+
Filetree_Item_Description ← (!'\n' Span)*
Filetree_Item_Children ← ('\n'? :'  ' [^\n]+)+ ↺ (Filetree_Item :'\n'?)+

List ↖ List_Ordered | List_Unordered
List_Ordered ← ( :[0-9]+ :'. ' List_Item :'\n' )+
List_Unordered ← (:'- ' List_Item :'\n'?)+
List_Item ← (('\n'+ :'  ')? [^\n])+
          ↺ (:'\n' | (Block_Prefixed | List_Item_Paragraph | Invalid_Block) )+
List_Item_Paragraph ← ( !('\n\n' | '\n- ') '\n'? (!'\n' (Newline|Span))+ )+

Table ← Table_Header Table_Body
Table_Header ← Table_Header_Item+ :'\n'?
Table_Header_Item ← :'|' Table_Colspan? :' '? (![|\n] Span)*
Table_Body ← (Table_Body_Row :'\n'?)*
Table_Body_Row ← !'\n' Table_Body_Row_Item (:'\n '? Table_Body_Row_Item)* :'|'?
Table_Body_Row_Item ← :'|'? Table_Colspan? (&(' ' | [^\n|] | '\n'+ '  ') :' '? [^\n|]* (('\n'+ :'  ') [^\n]*)* ↺ (:'\n' | Block)*)
Table_Colspan ← '|'+

Info ← (:'ℹ ' Block) | (:'ℹ\n' (!'\n\n' (:'\n' | Block))*)
Warning ← (:'⚠ ' Block) | (:'⚠\n' (!'\n\n' (:'\n' | Block))*)

Factsheet ← :'📍' (:' '? Factsheet_Thing)? :'\n' Factsheet_Facts?
Factsheet_Thing ← (!'\n' Span)*
Factsheet_Facts ← (:'- ' Factsheet_Fact :'\n'?)+
Factsheet_Fact ← (!'\n' Span)*

Paragraph ← ( (!'\n' (Newline|Span))+ '\n'? )+
Newline ← :'  \n'

Date_Label ← :'📆' ((Day :'/' Month :'/' Year) | (Month :'/' Year) | Year)
Day ← [0-3]? [0-9]
Month ← [0-1] [0-9]
Year ← :'20'? [0-9] [0-9]

Invalid_Block ← ( (!'\n' .)+ '\n')+

### Span Elements

Span ↖ Hover | Link | Emphasis | Image | Technical | Info_Span | Warning_Span | Iframe | Fit | Date_Label | Brand | .

Hover ← :'[' Hover_Content :']((' Link_Url :'))'
Hover_Content ← (!']' Span)+

Link ↖ Link_Inline | Link_Reference | Link_Auto
Link_Inline ← :'[' Link_Text :'](' Link_Url :')'
Link_Reference ← :'[' Link_Text :'][]'
Link_Auto ← :'<' [^ ] [^>]* :'>'
Link_Text ← (!']' Span)*
Link_Url ← (![)\n] .)*

Emphasis ↖ Emphasis_Bold  | Emphasis_Italic
Emphasis_Bold ← :'**' (!'**' Span)+ :'**'
Emphasis_Italic ← :'*' (!'*' Span)+ :'*'

Image ← :'![' Image_Alt :'](' Image_Url :')'
Image_Alt ← (!']' Span)*
Image_Url ← (!')' .)+

Technical ↖ Apielement | Keystroke | Path | Code | Terminal
Apielement ← :'@' [^ \n]+
Keystroke ← :'⌨' (Key :' '?)+
Key ← [^  \n]+
Path ← :'↑' (Fit | [^ \n])+
Code ← :'`' [^`\n]+ :'`'
Terminal ← :'└▪' [^↵\n]+ :'↵'

Iframe ← :'⎡' [^ \n]+

Brand ← [a-zA-Z0-9 ]+ :'™'

Info_Span ← :'ℹ' ![ \n] (!'\n' Span)+
Warning_Span ← :'⚠' ![ \n] (!'\n' Span)+

Fit ← :'↤' (Fit_Item | ![\n↦] Span)+ :'↦'
Fit_Item ← :'⭲' (![\n⭰] Span)+ :'⭰'
